BACKUP ~faiths_and_powers/backup~
AUTHOR ~SubtleD and Grammarsalad~


VERSION ~0.78sd1~


//README ~faiths_and_powers/readme-pnp.html~


ASK_EVERY_COMPONENT


ALWAYS

	ACTION_IF NOT FILE_EXISTS ~override/d5_fnp_settings.ini~ BEGIN
  	  COPY ~faiths_and_powers/d5_fnp_settings.ini~ ~override~
	END
 
    ACTION_IF ((!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_OBSCURING_MIST~)) AND (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~)) BEGIN
	   INCLUDE ~faiths_and_powers/lib/add_ids.tpa~
    END

  	OUTER_SPRINT ~mod_folder~ ~faiths_and_powers~

  	INCLUDE ~override/d5_fnp_settings.ini~

	INCLUDE ~faiths_and_powers/lib/misc_functions.tpa~

	INCLUDE ~faiths_and_powers/lib/bg2fp_effect_batches_fnp.tpa~

	INCLUDE ~faiths_and_powers/lib/fl#add_kit_ee.tpa~

//	INCLUDE ~faiths_and_powers/lib/spell_clone_effects.tpa~
	
	INCLUDE ~faiths_and_powers/lib/joinable_npcs.tpa~

	INCLUDE ~faiths_and_powers/lib/fnp_compat.tpa~
	
	ACTION_IF FILE_EXISTS_IN_GAME ~a#ooze.2da~ BEGIN
	  COPY_EXISTING ~alignmnt.2da~ ~override~
		REPLACE_TEXTUALLY ~DRUID.+$~ ~DRUID	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~FIGHTER_DRUID.+$~ ~FIGHTER_DRUID	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~TOTEMIC_DRUID.+$~ ~TOTEMIC_DRUID	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~SHAPESHIFTER.+$~ ~SHAPESHIFTER	0	1	0	1	1	1	0	1	0 ~
		REPLACE_TEXTUALLY ~BEAST_FRIEND.+$~ ~BEAST_FRIEND	0	1	0	1	1	1	0	1	0 ~
	END

	COPY ~faiths_and_powers/kits/clerics/B_LOR.spl~ ~override~

// joinable NPC issues for tutu/bgt/eet:
  ACTION_IF GAME_IS ~bgee~ BEGIN
    OUTER_SPRINT tutu_var ~~
    OUTER_SPRINT imoen6_var ~imoen6~
    OUTER_SPRINT jaheir7_var ~jaheir7~
    OUTER_SPRINT minsc7_var ~minsc7~
    OUTER_SPRINT quayle_var ~quayle~
    OUTER_SPRINT viconi6_var ~viconi6~
    OUTER_SPRINT xan_var ~xan~
    OUTER_SPRINT xzar_var ~xzar~
  END ELSE ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SPRINT tutu_var ~~
    OUTER_SPRINT imoen6_var ~imoen6_~
    OUTER_SPRINT jaheir7_var ~jaheir7_~
    OUTER_SPRINT minsc7_var ~minsc7_~
    OUTER_SPRINT quayle_var ~quayle_~
    OUTER_SPRINT viconi6_var ~viconi6_~
    OUTER_SPRINT xan_var ~xan_~
    OUTER_SPRINT xzar_var ~xzar_~
  END ELSE ACTION_IF GAME_IS ~bgt~ BEGIN
    OUTER_SPRINT tutu_var ~~
    OUTER_SPRINT imoen6_var ~imoen61~
    OUTER_SPRINT quayle_var ~bgquayle~
    OUTER_SPRINT viconi6_var ~viconi61~
    OUTER_SPRINT xan_var ~bgxan~
    OUTER_SPRINT xzar_var ~bgxzar~
  END ELSE ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
    OUTER_SPRINT tutu_var ~_~
    OUTER_SPRINT imoen6_var ~imoen6~
    OUTER_SPRINT quayle_var ~quayle~
    OUTER_SPRINT viconi6_var ~viconi6~
    OUTER_SPRINT xan_var ~xan~
    OUTER_SPRINT xzar_var ~xzar~
  END

END

// ASK_EVERY_COMPONENT

LANGUAGE
  ~English~
  ~english~
  ~faiths_and_powers/language/english/setup.tra~
  ~faiths_and_powers/language/english/IWD_spells.tra~
  ~faiths_and_powers/language/english/new_spells.tra~
  ~faiths_and_powers/language/english/HLA.tra~
//___________________________________________________________________________________




//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							CHOOSE SPHERE SYSTEM
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @21 // FnP original sphere system
DESIGNATED 21
SUBCOMPONENT @20 
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee~) @8

COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_spheres.d5~
COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_fnp_spheres.d5~

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  DELETE ~override/fnp_sphere_list.tpa~
END

//change shaman known spells________________________________________________________
//
LAF shaman_spells END
//__________________________________________________________________________________

/*
//Add IWD spells if not already in game_____________________________________________
//
ACTION_CLEAR_ARRAY aoe_spell

ACTION_IF (!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_CURSE~)) BEGIN  //if not IWDEE priest spells..from any source
 COPY ~%mod_folder%/compatibility/b_IWD.itm~ ~override~   	//to detect this component  Needed anymore???
 COPY ~%mod_folder%/compatibility/b_IWD.itm~ ~override/B_pri.itm~    //Needed anymore???
  INCLUDE ~%mod_folder%/data/components/Setup_IWD_Spells.tpa~      //install priest spells
END

//special, 'older' versions of SR and IWDee
ACTION_IF GAME_IS ~iwdee~ BEGIN
  ACTION_IF ((!FILE_CONTAINS_EVALUATED(~spell.ids~ ~CLERIC_BEAST_CLAW~)) AND (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~)) BEGIN
   INCLUDE ~faiths_and_powers/lib/iwdee_sr_readd.tpa~
  END
END

//special: reinstall spells if any version of SR installed
  ACTION_IF FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~ BEGIN
   INCLUDE ~faiths_and_powers/lib/sr_readd.tpa~
  END
//add new spells____________________________________________________________________


INCLUDE ~%mod_folder%/lib/fnp_wiz.tpa~
INCLUDE ~%mod_folder%/lib/fnp_new_spells.tpa~

//Now, Immunities.  DO AT THE VERY END OF ALL SPELL INSTALLS
INCLUDE ~%mod_folder%/lib/immunities.tpa~

/*
//sr spell deflection compatibility
ACTION_IF MOD_IS_INSTALLED ~spell_rev.tp2~ ~55~ BEGIN
  INCLUDE ~%mod_folder%/lib/compatibility_SR.tpa~
END
*/
//__________________________________________________________________________________
*/

ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
//	INCLUDE ~faiths_and_powers/lib/sphere_list_base.tpa~
	COPY ~faiths_and_powers/lib/sphere_list_base.tpa~ ~override/fnp_sphere_list.tpa~

END
ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
  ACTION_IF GAME_IS ~iwdee~ BEGIN
//	INCLUDE ~faiths_and_powers/lib/sphere_list_iwd_sr.tpa~
	COPY ~faiths_and_powers/lib/sphere_list_iwd_sr.tpa~ ~override/fnp_sphere_list.tpa~

  END
  ELSE BEGIN
//	INCLUDE ~faiths_and_powers/lib/sphere_list_sr.tpa~
	COPY ~faiths_and_powers/lib/sphere_list_sr.tpa~ ~override/fnp_sphere_list.tpa~
  END
END

INCLUDE ~override/fnp_sphere_list.tpa~

INCLUDE ~faiths_and_powers/lib/sphere_system.tpa~

LAF create_spheres END

//__________________________________________________________________________________
//__________________________________________________________________________________
//
BEGIN @22 // 2E-style sphere system
DESIGNATED 22
SUBCOMPONENT @20

COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_spheres.d5~
COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_2e_spheres.d5~

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  DELETE ~override/fnp_sphere_list.tpa~
END

//change shaman known spells________________________________________________________
//
LAF shaman_spells END
//__________________________________________________________________________________

COPY ~faiths_and_powers/lib/sphere_list_2E.tpa~ ~override/fnp_sphere_list.tpa~

INCLUDE ~override/fnp_sphere_list.tpa~

INCLUDE ~faiths_and_powers/lib/sphere_system.tpa~

LAF create_spheres END

//__________________________________________________________________________________
//__________________________________________________________________________________
//
BEGIN @23 // player-supplied custom sphere system
DESIGNATED 23
SUBCOMPONENT @20 (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~)

COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_spheres.d5~
COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_custom_spheres.d5~

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN

//change shaman known spells________________________________________________________
//
LAF shaman_spells END
//__________________________________________________________________________________

  INCLUDE ~override/fnp_sphere_list.tpa~
  INCLUDE ~faiths_and_powers/lib/sphere_system.tpa~
  LAF create_spheres END
END
ELSE BEGIN
  PRINT ~There is no sphere system defined! Please put a version of "fnp_sphere_list.tpa" into the override directory and try again.~
END

//__________________________________________________________________________________
//__________________________________________________________________________________
//
BEGIN @24 // no sphere system at all
DESIGNATED 24
SUBCOMPONENT @20

COPY ~faiths_and_powers/lib/markers/d5_marker.d5~ ~override/d5_no_spheres.d5~



//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						INSTALL SINGLE-CLASS KITS
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @31 // install cleric kits
DESIGNATED 31

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  INCLUDE ~override/fnp_sphere_list.tpa~
END
INCLUDE ~faiths_and_powers/lib/add_cleric_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_incarnate_kits.tpa~

LAM d5_kit_choices

ACTION_IF (GAME_IS ~iwdee~) BEGIN
   OUTER_SET d5_cleric_kelemvor =	0
   OUTER_SET d5_cleric_cyric 	=	0
END

// (clerics)
ACTION_IF (%d5_cleric_lathander% = 1) BEGIN
 LAF cleric_lathander END
END
ACTION_IF (%d5_cleric_torm% = 1) BEGIN
 LAF cleric_torm END
END
ACTION_IF (%d5_cleric_tymora% = 1) BEGIN
 LAF cleric_tymora END
END
ACTION_IF (%d5_cleric_helm% = 1) BEGIN
 LAF cleric_helm END
END
ACTION_IF (%d5_cleric_kelemvor% = 1) BEGIN
  LAF cleric_kelemvor END
END
ACTION_IF (%d5_cleric_tempus% = 1) BEGIN
 LAF cleric_tempus END
END
ACTION_IF (%d5_cleric_leira% = 1) BEGIN
 LAF cleric_leira END
END
ACTION_IF (%d5_cleric_baravar% = 1) BEGIN
 LAF cleric_baravar END
END
ACTION_IF (%d5_cleric_loviatar% = 1) BEGIN
 LAF cleric_loviatar END
END
ACTION_IF (%d5_cleric_talos% = 1) BEGIN
 LAF cleric_talos END
END
ACTION_IF (%d5_cleric_cyric% = 1) BEGIN
  LAF cleric_cyric END
END
ACTION_IF (%d5_cleric_umberlee% = 1) BEGIN
 LAF cleric_umberlee END
END
//
// (priests)
ACTION_IF (%d5_priest_ilmater% = 1) BEGIN
 LAF priest_ilmater END
END
ACTION_IF (%d5_priest_deneir% = 1) BEGIN
 LAF priest_deneir END
END
ACTION_IF (%d5_priest_sune% = 1) BEGIN
 LAF priest_sune END
END
ACTION_IF (%d5_priest_tempus% = 1) BEGIN
 LAF priest_tempus END
END
ACTION_IF (%d5_priest_mask% = 1) BEGIN
 LAF priest_mask END
END
ACTION_IF (%d5_priest_shar% = 1) BEGIN
 LAF priest_shar END
END
ACTION_IF (%d5_priest_moander% = 1) BEGIN
 LAF priest_moander END
END
ACTION_IF (%d5_priest_beshaba% = 1) BEGIN
 LAF priest_beshaba END
END
ACTION_IF (%d5_priest_baervan% = 1) BEGIN
 LAF priest_baervan END
END
//
/*
// (incarnates)
ACTION_IF (%d5_incarnate_mystra% = 1) BEGIN
 LAF incarnate_mystra END
END
*/

// (ur-priest)
ACTION_IF (%d5_ur_priest% = 1) BEGIN
 LAF cleric_ur_priest END
END


OUTER_SPRINT class ~C~
LAF remove_sc_menu_kits END


//__________________________________________________________________________________
//__________________________________________________________________________________
//
BEGIN @33 // install druid kits
DESIGNATED 33

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  INCLUDE ~override/fnp_sphere_list.tpa~
END
INCLUDE ~faiths_and_powers/lib/add_druid_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_mystic_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_occultist_kits.tpa~

//change druid class text____________________________________________________________
//
LAF druid_class_text END
//__________________________________________________________________________________

//druids on cleric spell/xp tables__________________________________________________
//
LAF druid_xp END
//__________________________________________________________________________________

//change shaman known spells________________________________________________________
//
ACTION_IF !(FILE_EXISTS_IN_GAME ~d5_no_spheres.d5)~) BEGIN
  LAF shaman_spells END
END
//__________________________________________________________________________________

LAM d5_kit_choices

ACTION_IF (FILE_EXISTS_IN_GAME ~d5_no_spheres.d5~) BEGIN
   OUTER_SET d5_earth_mystic 	=	0
   OUTER_SET d5_water_mystic 	=	0
   OUTER_SET d5_air_mystic 		=	0
   OUTER_SET d5_fire_mystic 	=	0
   OUTER_SET d5_light_mystic 	=	0
   OUTER_SET d5_shadow_mystic 	=	0
   OUTER_SET d5_elementalist 	=	0
END

// (druids)
ACTION_IF (%d5_forest_druid% = 1) BEGIN
 LAF forest_druid END
END
ACTION_IF (%d5_totemic_druid% = 1) BEGIN
 LAF totemic_druid END
END
ACTION_IF (%d5_hivekeeper_druid% = 1) BEGIN
 LAF hivekeeper_druid END
END
ACTION_IF (%d5_northern_druid% = 1) BEGIN
 LAF northern_druid END
END
ACTION_IF (%d5_lost_druid% = 1) BEGIN
 LAF lost_druid END
END
/*
ACTION_IF (%d5_multiclass_druids% = 1) BEGIN
 LAF fighter_druid END
 LAF druid_mage END
 LAF druid_thief END
 LAF druid_ranger END
END
*/
ACTION_IF (%d5_forest_druid% + %d5_totemic_druid% + %d5_hivekeeper_druid% + %d5_northern_druid% + %d5_lost_druid% + %d5_multiclass_druids% > 0) BEGIN
 LAF druid_shapeshifting END
END
//
// (mystics)
ACTION_IF (%d5_earth_mystic% = 1) BEGIN
 LAF earth_mystic END
END
ACTION_IF (%d5_water_mystic% = 1) BEGIN
 LAF water_mystic END
END
ACTION_IF (%d5_air_mystic% = 1) BEGIN
 LAF air_mystic END
END
ACTION_IF (%d5_fire_mystic% = 1) BEGIN
 LAF fire_mystic END
END
ACTION_IF (%d5_light_mystic% = 1) BEGIN
 LAF light_mystic END
END
ACTION_IF (%d5_shadow_mystic% = 1) BEGIN
 LAF shadow_mystic END
END
//
// (occultists)
ACTION_IF (%d5_beast_lord% = 1) BEGIN
 LAF beast_lord END
END
ACTION_IF (%d5_elementalist% = 1) BEGIN
 LAF elementalist END
END

OUTER_SPRINT class ~D~
LAF remove_sc_menu_kits END


//__________________________________________________________________________________
//__________________________________________________________________________________
//
BEGIN @35 // install paladin kits
DESIGNATED 35

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  INCLUDE ~override/fnp_sphere_list.tpa~
END
INCLUDE ~faiths_and_powers/lib/add_champion_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_zealot_kits.tpa~

//ranger/paladin spell tables_______________________________________________________
//
LAF paladin_spell_tables END
//__________________________________________________________________________________

LAM d5_kit_choices

ACTION_IF (FILE_EXISTS_IN_GAME ~d5_no_spheres.d5~) BEGIN
   OUTER_SET d5_zealot_ilmater 	=	0
   OUTER_SET d5_zealot_corellon 	=	0
   OUTER_SET d5_zealot_sune 	=	0
   OUTER_SET d5_zealot_mystra 	=	0
   OUTER_SET d5_zealot_eilistraee 	=	0
   OUTER_SET d5_zealot_gruumsh 	=	0
   OUTER_SET d5_zealot_talos 	=	0
   OUTER_SET d5_zealot_kossuth 	=	0
   OUTER_SET d5_zealot_garagos 	=	0
END

ACTION_IF (%d5_champion_tyr% + %d5_champion_azuth% + %d5_champion_moradin% + %d5_champion_kelemvor% + %d5_champion_tempus% + %d5_champion_bane% + %d5_champion_helm% + %d5_champion_talos% + %d5_champion_red% > 0) BEGIN
 //any race can have their pallys
 COPY_EXISTING ~clsrcreq.2da~ ~override~ //Testing...
 //pally
    SET_2DA_ENTRY 29 2 8 1
    SET_2DA_ENTRY 29 3 8 1
    SET_2DA_ENTRY 29 4 8 1
    SET_2DA_ENTRY 29 5 8 1
    SET_2DA_ENTRY 29 6 8 1
    SET_2DA_ENTRY 29 7 8 1
 //b guard
    SET_2DA_ENTRY 33 2 8 1
    SET_2DA_ENTRY 33 3 8 1
    SET_2DA_ENTRY 33 4 8 1
    SET_2DA_ENTRY 33 5 8 1
    SET_2DA_ENTRY 33 6 8 1
    SET_2DA_ENTRY 33 7 8 1
 BUT_ONLY_IF_IT_CHANGES
 //Dwarves need to be able to take the champion class.  Cha 13 requirement
 COPY_EXISTING ~ABCLASRQ.2da~ ~override~ //
 //Pally
    SET_2DA_ENTRY 6 6 6 13
    SET_2DA_ENTRY 30 6 6 13
    SET_2DA_ENTRY 31 6 6 13
    SET_2DA_ENTRY 32 6 6 13
 BUT_ONLY_IF_IT_CHANGES
END

ACTION_IF (%d5_champion_tyr% = 1) BEGIN
 LAF champion_tyr END
END
ACTION_IF (%d5_champion_azuth% = 1) BEGIN
 LAF champion_azuth	 END	//azuth essentially replaces Inquisitor
END
ACTION_IF (%d5_champion_moradin% = 1) BEGIN
 LAF champion_moradin END
END
ACTION_IF (%d5_champion_kelemvor% = 1) BEGIN
 //champ of Kele effectively replaces UHunter. In IWDEE, the champ of mykrul is merely added (they're different enough conceptually to be seperate)
 ACTION_IF GAME_IS ~iwdee~ THEN BEGIN   //
  LAF iwdee_champion_kelemvor END
 END
 //kelevmor undead hunter for non-iwdee
 ACTION_IF GAME_IS ~bgee bg2ee eet~ THEN BEGIN   //
  LAF champion_kelemvor END
 END
END
ACTION_IF (%d5_champion_tempus% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No feasible way to make them not fall in IWDEE as yet
  LAF champion_tempus END
 END
END
ACTION_IF (%d5_champion_bane% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE as yet
  LAF champion_bane END
 END
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE as yet
  LAF iwdee_champion_bane END
 END
END
ACTION_IF (%d5_champion_helm% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE as yet
  LAF champion_helm END
 END
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil fallable pallys in IWDEE
  LAF iwdee_champion_helm END
 END
END
ACTION_IF (%d5_champion_talos% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE
  LAF champion_talos END
 END
 ACTION_IF NOT FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No Evil nonfallable pallys in IWDEE
  LAF iwdee_champion_talos END
 END
END
ACTION_IF (%d5_champion_red% = 1) BEGIN
 ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No feasible way to make them not fall in IWDEE without fallen.2da
  LAF champion_red END
 END
END
//
// (other paladins)
//
// (zealot)
ACTION_IF (%d5_zealot_ilmater% + %d5_zealot_corellon% + %d5_zealot_sune% + %d5_zealot_mystra% + %d5_zealot_eilistraee% + %d5_zealot_gruumsh% + %d5_zealot_talos% + %d5_zealot_kossuth% + %d5_zealot_garagos% > 0) BEGIN
 //sneaky sneaky
 COPY ~faiths_and_powers/kits/zealots/b_sneak.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk02.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk03.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk04.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk05.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk06.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk07.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk08.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk09.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk10.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk11.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk12.spl~ ~override~
 COPY ~faiths_and_powers/kits/zealots/b_snk13.spl~ ~override~
END

ACTION_IF (%d5_zealot_ilmater% = 1) BEGIN
 LAF zealot_ilmater END
END
ACTION_IF (%d5_zealot_corellon% = 1) BEGIN
 LAF zealot_corellon END
END
ACTION_IF (%d5_zealot_sune% = 1) BEGIN
 LAF zealot_sune END
END
ACTION_IF (%d5_zealot_mystra% = 1) BEGIN
 LAF zealot_mystra END
END
ACTION_IF (%d5_zealot_eilistraee% = 1) BEGIN
 LAF zealot_eilistraee END
END
ACTION_IF FILE_EXISTS_IN_GAME ~fallen.2da~ BEGIN  //No feasible way to make them not fall in IWDEE w/o fallen.2da
 ACTION_IF (%d5_zealot_gruumsh% = 1) BEGIN
  LAF zealot_gruumsh END
 END
 ACTION_IF (%d5_zealot_talos% = 1) BEGIN
  LAF zealot_talos END
 END
 ACTION_IF (%d5_zealot_kossuth% = 1) BEGIN
  LAF zealot_kossuth END
 END
 ACTION_IF (%d5_zealot_garagos% = 1) BEGIN
  LAF zealot_garagos END
 END
END  //End grummsh/talos/kossuth/garagos

OUTER_SPRINT class ~P~
LAF remove_sc_menu_kits END


//__________________________________________________________________________________
//__________________________________________________________________________________
//
BEGIN @37 // install ranger kits
DESIGNATED 37

ACTION_IF (FILE_EXISTS_IN_GAME ~fnp_sphere_list.tpa~) BEGIN
  INCLUDE ~override/fnp_sphere_list.tpa~
END
INCLUDE ~faiths_and_powers/lib/add_ranger_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_scout_kits.tpa~

//ranger/paladin spell tables_______________________________________________________
//
LAF ranger_spell_tables END
//__________________________________________________________________________________

LAM d5_kit_choices

ACTION_IF (%d5_forest_ranger% = 1) BEGIN
 LAF forest_ranger END
END
ACTION_IF (%d5_mountain_ranger% = 1) BEGIN
 LAF mountain_ranger END
END
ACTION_IF (%d5_jungle_ranger% = 1) BEGIN
 LAF jungle_ranger END
END
ACTION_IF (%d5_desert_ranger% = 1) BEGIN
 LAF desert_ranger END
END
ACTION_IF (%d5_arctic_ranger% = 1) BEGIN
 LAF arctic_ranger END
END
ACTION_IF (%d5_beast_master% = 1) BEGIN
 LAF beast_master END
END
//
// (scouts)
ACTION_IF (%d5_mage_hunter% = 1) BEGIN
 LAF mage_hunter END
END
ACTION_IF (%d5_barbarian_ranger% = 1) BEGIN
 LAF barbarian_ranger END
END
ACTION_IF (%d5_elven_archer% = 1) BEGIN
 LAF elven_archer END
END
/*
ACTION_IF (%d5_halfling_slinger% = 1) BEGIN
 LAF halfling_slinger END
END
*/

LAF more_ranger_alignments END

OUTER_SPRINT class ~R~
LAF remove_sc_menu_kits END


//__________________________________________________________________________________
//__________________________________________________________________________________
//
//					CHOOSE WHETHER TO CHANGE USABILITY/PROFICIENCY
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @91 // item usability
DESIGNATED 91

COPY ~faiths_and_powers/lib/markers/d5_fnp_usability.d5~ ~override~


//__________________________________________________________________________________
//__________________________________________________________________________________
//
//							APPLY SPHERE SYSTEM
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @95 // apply sphere systen
DESIGNATED 95
INSTALL_BY_DEFAULT
REQUIRE_FILE ~d5_spheres.d5~ @8
FORBID_COMPONENT ~faiths_and_powers.tp2~ ~24~ @8

//get ready for sphere system_______________________________________________________
//
ACTION_IF !(FILE_EXISTS_IN_GAME ~d5_no_spheres.d5~) BEGIN
  INCLUDE ~faiths_and_powers/lib/sphere_system.tpa~
END	
//__________________________________________________________________________________

//read in sphere system______________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~d5_fnp_spheres.d5~) BEGIN
 ACTION_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
	INCLUDE ~faiths_and_powers/lib/sphere_list_base.tpa~
 END
 ACTION_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
  ACTION_IF GAME_IS ~iwdee~ BEGIN
	INCLUDE ~faiths_and_powers/lib/sphere_list_iwd_sr.tpa~
  END
  ELSE BEGIN
	INCLUDE ~faiths_and_powers/lib/sphere_list_sr.tpa~
  END
 END
END
ACTION_IF (FILE_EXISTS_IN_GAME ~d5_2e_spheres.d5~) || (FILE_EXISTS_IN_GAME ~d5_custom_spheres.d5~) BEGIN
  INCLUDE ~override/fnp_sphere_list.tpa~
END
//__________________________________________________________________________________

//read in kit functions_____________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/add_cleric_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_incarnate_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_druid_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_mystic_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_occultist_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_champion_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_zealot_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_ranger_kits.tpa~
INCLUDE ~faiths_and_powers/lib/add_scout_kits.tpa~

INCLUDE ~faiths_and_powers/lib/default_kit_spheres.tpa~
LAF vanilla_kit_spheres END
LAF mod_kit_spheres END
LAF default_kit_spheres END
//__________________________________________________________________________________

//read fnp_compat info in order to apply sphere system/item usability_______________
//
LAM ~READ_FNP_KIT_INFO~
//__________________________________________________________________________________

//alter item usability______________________________________________________________
//
ACTION_IF (FILE_EXISTS_IN_GAME ~d5_fnp_usability.d5~) BEGIN
  INCLUDE ~faiths_and_powers/lib/item_usability.tpa~
  LAF change_usability END
  LAF apply_usability END
END
//__________________________________________________________________________________

//Apply Sphere System_______________________________________________________________
//
LAF apply_spheres END
//__________________________________________________________________________________

//Handle NPCs_______________________________________________________________________
//
INCLUDE ~faiths_and_powers/lib/npc_kits.tpa~
LAF npc_sc_kits END
//__________________________________________________________________________________

//universal spells available at CharGen_____________________________________________
//
LAF universal_spells END
//__________________________________________________________________________________



/*
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//						INSTALL MULTICLASS KITS
//__________________________________________________________________________________
//__________________________________________________________________________________

BEGIN @99 // install multiclass kits
DESIGNATED 99


//__________________________________________________________________________________
*/
